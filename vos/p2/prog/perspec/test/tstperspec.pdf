procedure
parm    mode    type=keyword count=(0:1) valid=(batch,nobatch,inter) default=batch
! Nov 15, 2012 - RJB
! TEST SCRIPT FOR PERSPEC
!
! tests IBIS-1 graphics files
!
! Vicar Programs:
!   ibis-gen ibis-list pltgraf
!
! External programs
!       gnuplot 4.6 or greater
!           
! Parameters:
!   mode - method for processing: 
!       1) batch provides no display 
!       2) interactive or nobatch is used for display requiring
!       user interaction. 
!           
!   In batch mode it produces files testx.eps by calling gnuplot
!       to create the encapsulated postscript file which can be
!       later viewed with ghostscript or gimp
!   In interactive or nobatch mode gnuplot is called with a window
!       manager for X11. The gnuplot display is killed by
!       a mouse click anywhere on the plot panel
!            
! External test data: 
!       <none>
!
! Output:    
!   GENed test.imga sets, .gpi and .eps files and intermediate 
!       tmp* files 
!   the *.gpi data produced by pltgraf are gnuplot scripts
!            

    refgbl $autousage
    refgbl $echo
body
    let $autousage="none"
    let _onfail="stop"
    let $echo="yes"

! Create a polygon consisting of a cube with a diamond to one side; this
! is an attempt to make a shape that will have a recognizable orientation.
    ibis-gen cube.pic NC=3 NR=26 'ibis-1 'row DATACOL=(1,2,3) +
        data=(1,1,1, 1,51,1, 51,51,1, 51,1,1, 1,1,1, 1,1,51, +
    1,51,51, 51,51,51, 51,1,51, 1,1,51, 0,0,0, 1,51,1, 1,51,51, 0,0,0, +
    51,51,1, 51,51,51, 0,0,0, 51,1,1, 51,1,51, 0,0,0, +
    36,41,26, 41,41,36, 36,41,46, 31,41,36, 36,41,26, 0,0,0)

! TEST 1 - plot genned data
    ibis-list cube.pic NC=3 NR=26 GR1DIM=3
    pltgraf cube.pic title="Plot of original data" dim=3 +
        plot=pgrf0 mode=axis xrange=(-10,60) yrange=(-10,60) +
        xlabel="X meters" ylabel="Y meters"

if (mode = "nobatch" or mode = "inter")
    ush gnuplot pgrf0.gpi
end-if

! TEST 2 - plot 1st reoriented data
! Plot the cube default style to an output file, then use pltgraf to display
! the file
    perspec cube.pic cube_perspective azimuth=30 distance=100 origin=(26,26,26)
    ibis-list cube_perspective NC=3 NR=26 GR1DIM=3
    pltgraf cube_perspective title="Perspective view of a cube - 30 degrees AZ" dim=3 +
            plot=pgrf1 mode=axis xrange=(-50,50) yrange=(-50,50) +
            xlabel="X meters" ylabel="Y meters"

if (mode = "nobatch" or mode = "inter")
    ush gnuplot pgrf1.gpi
end-if       

! TEST 3 - plot 2nd reoriented data
    perspec cube.pic cube_perspective2 azimuth=40 distance=100 origin=(26,26,26)
    ibis-list cube_perspective2 NC=3 NR=26 GR1DIM=3
    pltgraf cube_perspective2 title="Perspective view of a cube - 40 degrees AZ" dim=3 +
            plot=pgrf2 mode=axis xrange=(-50,50) yrange=(-50,50) +
            xlabel="X meters" ylabel="Y meters"
  
if (mode = "nobatch" or mode = "inter")
    ush gnuplot pgrf2.gpi
end-if
 
let $echo="no" 
end-proc

